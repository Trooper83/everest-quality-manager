
buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails.plugins:hibernate5:7.3.1"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.6"
    }
}

plugins {
    id 'idea'
    id 'war'
    id 'org.grails.grails-web' version '6.0.0'
    id 'com.bertramlabs.asset-pipeline' version '4.3.0'
    id 'org.grails.grails-gsp' version '6.0.0'
}

version "3.0.3"
group "com.everlution.everest"

repositories {
    maven { url "https://repo.grails.org/grails/core" }
    maven {
        url 'https://repo.maven.apache.org/maven2'
        name 'Maven Central'
    }
}

configurations {
    developmentOnly
    runtimeClasspath.extendsFrom(developmentOnly)
}


/* required due to failed faker dependency as documented
*  here: https://github.com/DiUS/java-faker/issues/327
*  can remove once fixed or switch to
*  https://www.datafaker.net/documentation/getting-started/#installation
*/
configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.module.toString() == 'org.yaml:snakeyaml') {
            details.artifactSelection {
                it.selectArtifact(DependencyArtifact.DEFAULT_TYPE, null, null)
            }
        }
    }
}

dependencies {
    developmentOnly('org.springframework.boot:spring-boot-devtools:2.7.18')
    implementation 'org.springframework.boot:spring-boot-starter-logging:2.7.18'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.7.18'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.7.18'
    implementation 'org.grails:grails-core:6.0.0'
    implementation 'org.grails:grails-web-boot:6.0.0'
    implementation 'org.grails:grails-logging:6.0.0'
    implementation 'org.grails:grails-plugin-rest:6.0.0'
    implementation 'org.grails:grails-plugin-databinding:6.0.0'
    implementation 'org.grails:grails-plugin-i18n:6.0.0'
    implementation 'org.grails:grails-plugin-services:6.0.0'
    implementation 'org.grails:grails-plugin-url-mappings:6.0.0'
    implementation 'org.grails:grails-plugin-interceptors:6.0.0'
    implementation 'org.grails.plugins:cache:7.0.0'
    implementation "org.grails.plugins:async"
    implementation 'org.grails.plugins:scaffolding:5.0.1'
    implementation 'org.grails.plugins:spring-security-core:6.1.1'
    implementation 'org.grails.plugins:spring-security-ui:4.0.0.M1'
    implementation "org.grails.plugins:spring-security-rest:3.0.1"
    implementation "org.grails.plugins:events"
    implementation 'org.grails.plugins:hibernate5:8.1.0'
    implementation 'org.hibernate:hibernate-core:5.6.1.Final'
    implementation 'org.grails.plugins:gsp:6.0.0'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'
    implementation 'org.apache.tomcat:tomcat-jdbc:10.1.19'
    console 'org.grails:grails-console:6.1.2'
    profile 'org.grails.profiles:web:5.0.5'
    //Migration of h2 to v2 is difficult (https://h2database.com/html/migration-to-v2.html)
    runtimeOnly 'com.h2database:h2:1.4.200'
    runtimeOnly 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    runtimeOnly 'com.bertramlabs.plugins:asset-pipeline-grails:4.3.0'
    testImplementation 'org.glassfish.web:el-impl:2.2'
    testImplementation 'org.grails:grails-gorm-testing-support:3.2.1'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.grails:grails-web-testing-support:3.2.1'
    testImplementation 'org.gebish:geb-spock:6.0'
    testImplementation "com.github.javafaker:javafaker:1.0.2"
    implementation  'org.springframework.boot:spring-boot-starter-tomcat:2.7.18'
    testImplementation 'com.konghq:unirest-java:3.14.5'
    testImplementation 'com.konghq:unirest-object-mappers-gson:3.14.5'
    testImplementation 'org.seleniumhq.selenium:selenium-remote-driver:3.141.59'
    testImplementation 'org.seleniumhq.selenium:selenium-api:3.141.59'
    testImplementation 'org.seleniumhq.selenium:selenium-support:3.141.59'
    testRuntimeOnly 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always',
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m',
        '-Duser.timezone=UTC')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

compileGroovy {
    configure {
        groovyOptions.forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

test {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
    systemProperty "geb.env", "chrome"
    systemProperty "sendResults", "true"
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
}

assets {
    minifyJs = true
    minifyCss = true
}
